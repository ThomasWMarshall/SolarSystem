<html>
<meta http-equiv="content-type" content="text/html; charset=utf-8" />
<style>
canvas {
  padding: 0;
  margin: auto;
  display: block;
  position: absolute;
  top: 0;
  bottom: 0;
  left: 0;
  right: 0;
}
</style>
<head>
  <title>Solar System</title>

  <!--
    The scene vertex shader has the interesting job of implementing the
    specalized lighting model we are using, that treats point sources of finite
    distances as infinitely far away. It achieves this by forcing the
    calculation of the toLight vector to be calculated as the vector from the
    light source to the center of the current object as opposed to the position
    of the current vertex. Otherwise, this shader just takes care of things to 
    do with perspective rendring including passing texture coordinates on to
    the fragment shaders.
  -->
  <script id = "scene-vertex-shader" type = "x-shader/x-vertex">

  attribute vec4 vPosition;
  attribute vec2 texCoordIn;
  attribute vec4 vNormal;

  uniform mat4 pMatrix;
  uniform mat4 mvMatrix;
  uniform mat4 transform;

  uniform bool ignoreLightIn;

  varying vec3 norm;
  varying vec3 toLight;
  varying vec2 texCoord;
  varying float ignoreLight;

  void main() {
    norm = normalize(vec3(vNormal));
    toLight = normalize((transform * vec4(0.,0,0,-1)).xyz);
    gl_Position = pMatrix * mvMatrix * transform * vPosition;
    texCoord = texCoordIn;
    if (ignoreLightIn) {ignoreLight = 1.0;} else {ignoreLight = 0.0;}
  }

  </script>

  <!--
    The scene fragment shader takes interpolated normals from the scene vertex
    shader in order to achieve Phong shading. We multiply the value of the
    texture at a given point by a power of the cos of the angle between the
    vector to the light source and the normal vector. This achieves a visually
    pleasing lighting model.
  -->
  <script id = "scene-fragment-shader" type = "x-shader/x-fragment">

  precision highp float;

  varying vec3 norm;
  varying vec3 toLight;
  varying vec2 texCoord;
  varying float ignoreLight;

  uniform sampler2D uSampler;
  uniform vec4 vColor;

  void main() {
    float multiplier = (dot(toLight, norm) >= 0.0) ? pow(dot(toLight, norm),1.25): 0.0;
    if (ignoreLight > 0.5) {multiplier = 1.0;}
    gl_FragColor = vec4((multiplier * texture2D(uSampler, texCoord)).xyz, 1.0);
  }

  </script>

  <!--
    Arguably the simplest shader here, the texture vertex shader passes the
    verticies of a single quad onto the fragment shaders along with texture
    coordinates.
  -->
  <script id = "texture-vertex-shader" type = "x-shader/x-vertex">

  attribute vec4 vPosition;
  attribute vec2 texCoordIn;

  varying vec2 texCoord;

  void main() {
    gl_Position = vPosition;
    texCoord = texCoordIn;
  }

  </script>

  <!--
    This is the texture fragment shader. It simply renders a texture to the
    screen as a single textured quad by sampling the texture at each point.
  -->
  <script id = "texture-fragment-shader" type = "x-shader/x-fragment">

  precision highp float;

  varying vec2 texCoord;

  uniform sampler2D uSampler;

  void main() {
    gl_FragColor = texture2D(uSampler, texCoord);
  }

  </script>

  <!--
    This is the mixer fragment shader. It takes values from two different
    textures and adds them together before rendering the result.
  -->
  <script id = "mixer-fragment-shader" type = "x-shader/x-fragment">

  precision highp float;

  varying vec2 texCoord;

  uniform sampler2D uSampler1;
  uniform sampler2D uSampler2;

  void main() {
    gl_FragColor = vec4(texture2D(uSampler1, texCoord).rgb + 
                        texture2D(uSampler2, texCoord).rgb, 1.0);
  }

  </script>

  <!--
    This is the fragment shader that generates the star image. It is based
    on a shader made by mu6k on Shadertoy.com, which can be found here:

    https://www.shadertoy.com/view/4sX3Rs#

    I have taken inspiration from many of his expressions, although perhaps
    the most notable influence his work had on our project was the color
    of the star.

    The shader works by considering interpolated x and y values across a
    texture. Brightness values can be determined for each of these points
    given just the star's point on the screen and the brightness of the
    image. After the initial variables are calculated, the color variable
    goes through a series of additions, all of which contribute things
    bloom, lens flare, etc. For the randomized elements of the rendering,
    we use a noise texture.

    The stars point on the screen is determined by sending just the
    origin through the perspective rendering transformations.
  -->

  <script id = "lens-fragment-shader" type = "x-shader/x-fragment">

  precision highp float;

  varying vec2 texCoord;

  uniform sampler2D noiseSampler;
  uniform float aspect;

  uniform float brightness;

  uniform bool forcePoint;
  uniform vec3 forcePointVec;

  uniform mat4 pMatrix;
  uniform mat4 mvMatrix;

  void main() {
    const float PI = 3.141592653589793238462643383;

    vec4 point = vec4(0.0,0.0,0.0,1.0);

    if (forcePoint) {point = vec4(forcePointVec,1.0);}

    vec4 rawPos = (pMatrix * mvMatrix * point) / (pMatrix * mvMatrix * point).w;

    vec2 mainPos = rawPos.xy;

    vec2 uv = (texCoord  - vec2(0.5,0.5)) * 2.0;

    vec2 uvd = uv  * length(uv);
    
    vec2 mainVector = uv-mainPos;
    float angle = atan(mainVector.x, mainVector.y) + PI;
    float n = texture2D(noiseSampler, vec2(angle / (2.0*PI) / 8.0 + (mainPos.x + mainPos.y) / 128.0) + vec2(1.0,1.0)/128.0).x;

    vec3 color = vec3(0.1/pow(length(mainVector),0.7));

    color += color * (sin(n * 8.0) / 20.0 + 0.5 + n * 0.25);

    vec2 uvx = mix(uv, uvd, -0.5);
    color += max(0.0,0.01-pow(length(uvx+0.2*mainPos),1.9))*7.0;
    color += max(0.0,0.01-pow(length(uvx+0.3*mainPos),1.9))*10.0;

    color += max(0.0,0.01-pow(length(uvx+1.4*mainPos),1.2))*15.0;
    color += max(0.0,0.01-pow(length(uvx+1.5*mainPos),1.4))*10.0;

    color += max(0.0,0.01-pow(length(uvx-0.7*mainPos),3.3))*10.0;
    color += max(0.0,0.01-pow(length(uvx-0.6*mainPos),2.0))*10.0;

    color += max(0.0,0.01-pow(length(uvx+2.5*mainPos),1.9))*4.0;
    color += max(0.0,0.01-pow(length(uvx+3.5*mainPos),1.9))*2.0;

    color -= vec3(length(uvd) * 0.05);

    if (mainPos.x >  1.0) {color *= max(0.0, 2.0 - mainPos.x);}
    if (mainPos.x < -1.0) {color *= max(0.0, 2.0 + mainPos.x);}

    if (mainPos.y >  1.0) {color *= max(0.0, 2.0 - mainPos.y);}
    if (mainPos.y < -1.0) {color *= max(0.0, 2.0 + mainPos.y);}

    if (rawPos.z > 1.0) {color *= 0.0;}

    color *= brightness;

    gl_FragColor = vec4(vec3(1.4, 1.2, 1.0) * color, 1.0);
    }

  </script>

  <script type="text/javascript" src="../Common/webgl-utils.js"></script>
  <script type="text/javascript" src="../Common/initShaders.js"></script>
  <script type="text/javascript" src="../Common/MV.js"></script>
  <script type="text/javascript" src="sphere.js"></script>
  <script type="text/javascript" src="square.js"></script>
  <script type="text/javascript" src="solarsystem.js"></script>

</head>
<body bgcolor = $000000>

  <canvas id="gl-canvas" width="1024" height="1024">
    Error: Canvas element failed to load.
  </canvas>

</body>
</html>
